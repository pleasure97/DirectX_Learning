/////////////
// GLOBALS //
/////////////
Texture2D shaderTexture : register(t0); 
Texture2D depthMapTexture : register(t1);
SamplerState SampleTypeClamp : register(s0); 
SamplerState SampleTypeWrap : register(s1);

//////////////
// TYPEDEFS //
//////////////

struct PixelInputType
{
	float4 position : SV_POSITION; 
	float2 tex : TEXCOORD0; 
	float3 normal : NORMAL; 
	float4 lightViewPosition : TEXCOORD1; 
};

//////////////////////
// CONSTANT BUFFERS //
//////////////////////

cbuffer LightBuffer
{
	float4 ambientColor; 
	float4 diffuseColor; 
	float3 lightDirection; 
	float bias; 
};

////////////////////////////////////////////////////////////////////////////////
// Pixel Shader
////////////////////////////////////////////////////////////////////////////////

float4 ShadowPixelShader(PixelInputType input) : SV_TARGET
{
	float4 color; 
	float2 projectTexCoord; 
	float depthValue; 
	float lightDepthValue; 
	float lightIntensity; 
	float4 textureColor; 
	float3 lightDir; 

	// Invert the light direction.
	lightDir = -lightDirection; 

	// Set the default output color to the ambient light value for all pixels.
	color = ambientColor; 

	// Calculate the projected texture coordinates.
	projectTexCoord.x = input.lightViewPosition.x / input.lightViewPosition.w / 2.f + 0.5f; 
	projectTexCoord.y = -input.lightViewPosition.y / input.lightViewPosition.w / 2.f + 0.5f; 

	// Determine if the projected coordinates are in the 0 to 1 range. 
	// If it is, then pixel is inside the projected viewport.
	if ((saturate(projectTexCoord.x) == projectTexCoord.x) && (saturate(projectTexCoord.y) == projectTexCoord.y))
	{
		// Sample the shadow map depth value from the depth texture using the sampler at the projected texture coordinate location.
		depthValue = depthMapTexture.Sample(SampleTypeClamp, projectTexCoord).r; 

		// Calculate the depth of the light.
		lightDepthValue = input.lightViewPosition.z / input.lightViewPosition.w; 

		// Subtract the bias from the lightDepthValue. 
		lightDepthValue -= bias; 

		// Compare the depth of the shadow map and the depth of the light to determine whether to shadow or to light this pixel.
		// If the light is in front of the object, then light the pixel, if not, then shadow this pixel since an object (oclluder) is casting a shadow on it.
		if (lightDepthValue < depthValue)
		{
			// Calculate the amount of light on this pixel.
			lightIntensity = saturate(dot(input.normal, lightDir)); 

			if (lightIntensity > 0.f)
			{
				// Determine the final diffuse color based on the diffuse color and the amount of light intensity.
				color += diffuseColor * lightIntensity; 

				// Saturate the final color. 
				color = saturate(color); 
			}
		}
	}
	else
	{
		// If this is outside the area of the shadow map, then draw things normally with regular lighting.
		lightIntensity = saturate(dot(input.normal, lightDir)); 
		if (lightIntensit > 0.f)
		{
			color += diffuseColor * lightIntensity; 
			color = saturate(color); 
		}
	}

	// Sample the pixel color from the texture using the sampler at this texture coordinate location.
	textureColor = shaderTexture.Sample(SampleTypeWrap, input.tex); 

	// Combine the light and texture color.
	color *= textureColor;

	return color; 
}
